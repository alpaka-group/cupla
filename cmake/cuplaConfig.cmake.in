#
# Copyright 2021 Simeon Ehrig
#
# This file is part of cupla.
#
# cupla is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# cupla is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with cupla.
# If not, see <http://www.gnu.org/licenses/>.
#

@PACKAGE_INIT@

check_required_components("@PROJECT_NAME@")

option(CUPLA_STREAM_ASYNC_ENABLE "Enable asynchronous streams" ON)

# the minimum and maximum supported alpaka version are fixed during the installation of cupla
set(_CUPLA_MIN_ALPAKA_VERSION @_CUPLA_MIN_ALPAKA_VERSION@)
set(_CUPLA_UNSUPPORTED_ALPAKA_VERSION @_CUPLA_UNSUPPORTED_ALPAKA_VERSION@)

find_package(alpaka ${_CUPLA_MIN_ALPAKA_VERSION} REQUIRED HINTS $ENV{ALPAKA_ROOT})
if(alpaka_VERSION VERSION_GREATER_EQUAL _CUPLA_UNSUPPORTED_ALPAKA_VERSION)
  message(WARNING "Unsupported alpaka version ${alpaka_VERSION}. "
    "Supported versions [${_CUPLA_MIN_ALPAKA_VERSION},${_CUPLA_UNSUPPORTED_ALPAKA_VERSION}).")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/addExecutable.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/cuplaTargetHelper.cmake")

# restore the CMAKE_INSTALL_PREFIX of the installation of cupla
set(_RESTORED_CUPLA_INSTALL_PRFIX "${CMAKE_CURRENT_LIST_DIR}/../../../")
# The cupla target must be created with cupla at project configuration time.
# Some functions in the cupla source files are specialised with the ACC template parameter.
# Compiling the source files must be done at build time of the project. If this were done at
# cupla installation time, the ACC would be set once at cupla installation time for all
# projects using that installation of cupla.
createCuplaTarget(@PROJECT_NAME@
  ${_RESTORED_CUPLA_INSTALL_PRFIX}/include # include directory path
  ${_RESTORED_CUPLA_INSTALL_PRFIX}/src/cupla # src directory path
  )
